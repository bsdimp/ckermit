; File CKERMOD.INI, Sample C-Kermit 5A(190) customization file.
;
; This file, which is ONLY A SAMPLE, should be called:
;
;   .mykermrc   (UNIX, OS-9, Aegis)
;   CKERMOD.INI (VMS, OpenVMS, AOS/VS, OS/2, Amiga, Atari ST)
;   ckermod.ini (Stratus VOS)
;
; MODify it to suit your needs and preferences, and install it in the same
; directory as your C-Kermit initialization file.  The design of this file
; lets you fill in a section for each different operating system where you
; run C-Kermit.  This file is executed automatically by the standard C-Kermit
; initialization file, CKERMIT.INI (or .kermrc).
;
; Authors:  Christine Gianone and Frank da Cruz, Columbia University.
; Date:     23 November 1992 for C-Kermit 5A(188).
; Modified: 30 June 1993 for edit 189.
;           23 August 1994 for edit 190.

ECHO Executing SAMPLE C-Kermit customization file \v(cmdfile)...
ECHO { Please edit this file to reflect your needs and preferences.}
;
; ... and then remove the ECHO commands above.

COMMENT - Settings that apply to all the systems I use:
;
set delay 1                     ; I escape back quickly
set dial display on             ; I like to watch C-Kermit dial
set window 2                    ; Use 2 window slots
set block 3                     ; Use 16-bit CRC error checking        
set receive packet-length 1000  ; Use 1000-character Kermit packets 

; The remaining commands in this file depend on the script programming
; language.  Here we make sure we have it, then we use it to go to the
; section that applies to the type of system where C-Kermit is running:
;
set take error on               ; Make errors fatal temporarily
check if                        ; Do we have an IF command?
set take error off              ; Yes we do, back to normal

check FORWARD
if success forward \v(system)
else goto \v(system)            ; Go execute system-dependent commands

:UNIX                           ; UNIX, all versions...
set delay 1                     ; I escape back quickly
if > \v(version) 501189 if remote end
set terminal apc off            ; Change to ON if desired
def mydial set modem hayes, -   ; Define a macro for dialing out...
   set line /dev/acu, -
   if fail end 1, -
   set speed 2400, -
   if fail end 1, -
   dial \%1, -
   if fail end 1, -
   connect
def nasa -                      ; Get latest news from NASA
   telnet spacelink.msfc.nasa.gov
end                             ; End of UNIX section

:VMS                            ; VMS and OpenVMS
define on_exit -                ; Print a message when Kermit exits
   echo Returning you to \v(system) now.
if > \v(version) 501189 if remote end
set terminal apc off            ; Change to ON if desired
def mydial set modem telebit, - ; Sample macro for dialing out...
   set line txa5, -
   if fail end 1, -
   set speed 19200, -
   if fail end 1, -
   dial \%1, -
   if fail end 1, -
   connect
end                             ; End of VMS section

:OS/2                           ; OS/2 customizations...
set modem hayes                 ; I have a Hayes modem
set port com1                   ; on communication port 1
set speed 19200                 ; at this speed
set flow rts/cts                ; It uses hardware flow control
set terminal apc off            ; Change to ON if desired
set server display on           ; Show file display in server mode too
;
; OS/2 character sets, change if necessary.  In OS/2, your current PC code
; page is used automatically as the FILE character-set and as the local half
; of your TERMINAL character-set.
;
set command byte 8              ; Use 8 bits between Kermit and console
set xfer char latin1            ; Use Latin-1 for text file transfer
set term char latin1            ; And use Latin-1 during CONNECT mode
;
; Macro for "ANSI" terminal emulation
;
define ANSI set term type ansi

; Macro for setting terminal rows
;
define ROWS run mode co80\{44}\%1

echo { See OS/2 section for sample key mappings and screen colors.}
;
; Sample OS/2 key redefinitions.  As distributed, they are
; "commented out" by the leading semicolon.  To "uncomment" a command,
; remove the semicolon from the left margin.  Add, remove, or change
; key mapping commands to suit your needs and preferences.
; See pages 428-431 of "Using C-Kermit" for details, plus CKERMIT.UPD,
; and see CKOVTK2.INI for a complete VT220 key mapping.
;
; set key \27 \96               ; Swap the Escape and Accent-Grave keys
; set key \96 \27               ; ...during CONNECT mode.
; set key \127 \8               ; Backspace key sends BS rather than DEL.

COMMENT - Terminal emulation screen colors:
;
; TERMINAL-SCREEN   = Terminal emulation screen (synonym: "normal")
; BORDER            = Border around terminal screen (fullscreen sessions only)
; HELP-SCREEN       = Popup help screens
; STATUS-LINE       = Status line
; UNDERLINED-TEXT   = Color for simulating underlined text
;
COMMENT - Sample color setup, change to suit your preferences...
set terminal color border green                ; green border
set terminal color terminal-screen blue lgray  ; blue on light gray
set terminal color underline black lgray       ; black on light gray
set terminal color status green blue           ; green on blue
set terminal color help white cyan             ; white on cyan

; COPY command...
define  copy if < argc 2 fatal {Copy what?},-
  if < argc 3 fatal {Copy \%1 to what?},-
  run copy \%1 \%2

COMMENT - ALTERNATIVE DIAL PROCEDURE
; Sample macro showing how to add a new modem type, in this case
; Practical Peripherals PM14400FXMT - see CKERMIT.UPD for discussion.
define PPDIAL -
  set modem hayes, - ; Build on basic Hayes modem type.
  set port 1, -      ; Change or remove this if necessary.
  set flow none, -
  set speed 57600, -
  set dial speed-matching off, -
  set dial init ATQ0W1S95=47&D2X4S2=43N1S37=11&K3&Q5S36=7S46=2S82=128\13, -
  dial \%1, -
  if success set flow rts, -
  else end 1

COMMENT - XSEND macro sends a directory tree.  Read XSEND.CMD for details.
; Arguments:
; 1. Pathname, such as C:\\ (backslashes must be doubled!)
; 2. Filespec, such as *.*
;
define XSEND rexx call \v(startup)xsend.cmd '\%1 \%2'

COMMENT - UPDATES displays CKERMIT.INF file with Viewer
define updates run view ckermit.inf

COMMENT - Access to external protocols
;
; These are commented out.  If you need them, and if you have the
; software (M2ZMODEM or P), uncomment the appropriate group of commands.
; Read the CKERMIT.UPD file for further information.

; M2ZMODEM program, external protocol
; take \m(_inidir)m2zmodem.ini

; P v.1.01 program, external protocol
; take \m(_inidir)p101.ini

; P v.2.00 program, external protocol
; take \m(_inidir)p200.ini

; CEXYZ/2 program, external protocol
; take \m(_inidir)cexyz2.ini
end                             ; End of OS/2 section

:OS9/68K                        ; OS-9/68000
if > \v(version) 501189 if remote end
set line /t3                    ; I want to use /t3 at 9600 bps
xif success { -
    set speed 9600, -
    echo { Communications line set to /t3, 9600 bps} -
}
end                             ; End of OS-9 section

:AOS/VS                         ; Data General AOS/VS
if > \v(version) 501189 if remote end
def mydial set modem hayes, -   ; Macro for dialing out with Hayes modem...
   set line @con3, -            ; Only works if modem is on @con3
   if fail end 1, -
   set speed 2400, -            ; and is set for 2400bps
   if fail end 1, -
   dial \%1, -
   if fail end 1, -
   connect
end

; And so on, you get the idea...
; Fill in the sections that apply to you.

:Stratus_VOS			; Stratus VOS
:Amiga                          ; Commodore Amiga
:Atari_ST                       ; Atari ST
:Macintosh                      ; Apple Macintosh
:unknown                        ; Others

; (End of CKERMOD.INI)
